/*
 * gradle build for house registration service.
 *
 */
buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath 'org.hidetake:gradle-ssh-plugin:2.9.0'
    }
}


plugins {
  id "com.srcclr.gradle" version "3.1.0"
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'org.hidetake.ssh'

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    maven {
        url myMavenRepoGwsReadUrl
    }

    jcenter()
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile "com.gws.addressauthenticator:gws.addressauthenticator:${aaVersion}"
    compile "com.gws.productionenvy.framework:gws.productionenvy.framework:${productionEnvyVersion}"
    compile "org.slf4j:slf4j-api:${slf4Version}"
    compile "io.dropwizard:dropwizard-core:${dropWizardVersion}"
    compile "io.dropwizard:dropwizard-views-mustache:${dropWizardVersion}"
    compile "io.dropwizard:dropwizard-assets:${dropWizardVersion}"
    compile "io.dropwizard:dropwizard-json-logging:${dropWizardVersion}"
    compile "com.google.inject:guice:${guiceVersion}"
    compile "com.google.inject.extensions:guice-assistedinject:${guiceVersion}"
    compile "org.mockito:mockito-all:${mockitoVersion}"
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile "org.mongodb:mongodb-driver:${mongoDriverVersion}"
    checkstyle "com.puppycrawl.tools:checkstyle:${checkstyleVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "com.tngtech.jgiven:jgiven-junit:${jgivenVersion}"
}

sourceSets {
    solitaryTests {
        java.srcDir file('src/solitary/java')
	compileClasspath += main.output
	runtimeClasspath += main.output
    }
    
    unitTests {
        java.srcDir file('src/unit/java')
	compileClasspath += main.output
	runtimeClasspath += main.output
    }
    
    acceptanceTests {
        java.srcDir file('src/acceptance/java')
	compileClasspath += main.output
	runtimeClasspath += main.output
    }
}

configurations {
    unitTestsCompile.extendsFrom testCompile
    unitTestsRuntime.extendsFrom testRuntime
    solitaryTestsCompile.extendsFrom testCompile
    solitaryTestsRuntime.extendsFrom testRuntime
    acceptanceTestsCompile.extendsFrom testCompile
    acceptanceTestsRuntime.extendsFrom testRuntime
}

task solitaryTests(type: Test) {
    testClassesDir = sourceSets.solitaryTests.output.classesDir
    classpath = sourceSets.solitaryTests.runtimeClasspath
}

task solitaryTestsJar(dependsOn: compileSolitaryTestsJava, type: Jar) {
    baseName = "${project.name}-sol"
    from sourceSets.solitaryTests.output.classesDir
}

task unitTests(type: Test) {
    testClassesDir = sourceSets.unitTests.output.classesDir
    classpath = sourceSets.unitTests.runtimeClasspath
}

task unitTestsJar(dependsOn: compileUnitTestsJava, type: Jar) {
    baseName = "${project.name}-ut"
    from sourceSets.unitTests.output.classesDir
}

task acceptanceTests(type: Test) {
    testClassesDir = sourceSets.acceptanceTests.output.classesDir
    classpath = sourceSets.acceptanceTests.runtimeClasspath
}

task acceptanceTestsJar(dependsOn: compileAcceptanceTestsJava, type: Jar) {
    baseName = "${project.name}-at"
    from sourceSets.acceptanceTests.output.classesDir
}

task printProps {
    doLast {
        println deployer_user
        println deployer_password
    }
}

mainClassName = 'com.gws.propertymgmt.portfoliomgmt.RegisterHouseService'

shadowJar {

    project.tasks.assemble.dependsOn project.tasks.shadowJar
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    classifier=null

    baseName = "${project.name}-${prjVersion}"
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }

    baseName = "${project.name}-${prjVersion}"
}

publishing {
    repositories {
        maven {
            url myMavenRepoGwsWriteUrl
            credentials {
                username System.getProperty("repoId")
                password System.getProperty("repoPassword")
            }
        }
    }
   
    publications {
        maven(MavenPublication) {
            groupId prjGroup
            artifactId prjArtifact
            version prjVersion
         
            artifact("$buildDir/libs/${prjArtifact}-${prjVersion}.jar")
        }
    }
}

checkstyle {
    toolVersion "${checkstyleVersion}"
    configFile file("gw-checkstyle.xml")
}

checkstyleMain {
    source ='src/main/java'
}

checkstyleTest {
    source =''
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events "passed", "skipped", "failed", "standardOut"
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat "full"
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }    
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet resources.text.fromFile('checkstyle-simple.xsl')
    }
}

task dockerCopy(type: Copy) {

    dockerCopy.configure {
       from('etc')
       into('docker/runtime_container')
       include('*')
    }

    dockerCopy.configure {
       from('build/libs')
       into('docker/runtime_container')
       include('*')
    }

    dockerCopy.configure {
       from('scripts')
       into('docker/runtime_container')
       include('*')
    }
}

// ssh deployment
ssh.settings {
  dryRun = project.hasProperty('dryRun')
}

remotes {
  staging {
    host = 'propertyhoncho.hopto.org'
    port = 5767
    user = deployer_user
    password = deployer_password
  }
}

task deploy() {
  doLast {
    ssh.run {
      session(remotes.staging) {
        // Execute a command
        def result = execute 'sudo service apache2 status'

        // Also Groovy methods or properties are available in a session closure
        println result
      }
    }
  }
}

task smoke() {
    println("smoke testing")
}

srcclr {
}
